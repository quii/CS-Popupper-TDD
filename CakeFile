fs         = require 'fs'
{exec}     = require 'child_process'
utils      = require './CakeFileHelpers'

less_dir = "public/stylesheets/less/"
client_coffee_dir = "client-coffee/"
compiled_coffee_path = "public/js/app.coffee"

client_coffee_files = utils.list_files_in_dir(client_coffee_dir)

task 'unit:test', 'Run unit tests', -> utils.runTest('test/unit')

task 'functional:test', 'Run functional test (node must be running)', -> utils.runTest('test/functional')

task 'watch:js', 'Watch prod source files and build changes', ->
    invoke 'build:js'
    utils.watch_and_run client_coffee_files, 'build:js'

task 'build:js', 'Build single application file from source files', ->
  appContents = new Array 
  remaining = client_coffee_files.length

  for file, index in client_coffee_files then do (file, index) ->
    fs.readFile file, 'utf8', (err, fileContents) ->
      throw err if err
      appContents[index] = fileContents
      process() if --remaining is 0
  process = ->
    fs.writeFile compiled_coffee_path, appContents.join('\n\n'), 'utf8', (err) ->
      throw err if err
      exec "coffee --compile #{compiled_coffee_path}", (err, stdout, stderr) ->
        if err
          console.log "Error compiling coffee file. #{err}"
        else
          fs.unlink compiled_coffee_path, (err) ->
            if err
              console.log 'Couldn\'t delete the app.coffee file/'
          invoke 'compress-client-js'

task 'watch:less', "Watches #{less_dir} for changes and recompiles", ->
  invoke "compile-less"
  utils.watch_and_run utils.list_files_in_dir(less_dir), "compile-less"

task 'compile-less', 'Compile and minify CSS', ->
  exec "lessc #{less_dir}main.less -yui-compress > public/stylesheets/style.css"

task 'compress-client-js', 'Use Uglify.js to compress the JS', ->
  console.log "Ugliying client side Javascript"
  exec 'uglifyjs -o public/js/app.min.js public/js/app.js'