fs         = require 'fs'
{exec}     = require 'child_process'
utils      = require './CakeFileHelpers'

less_dir = "public/stylesheets/less/"

appFiles  = [
  #put your client coffee files inside "client-coffee" directory
  #add them to this array in the order you want them to be compiled
]

task 'unit:test', 'Run unit tests', -> utils.runTest('test/unit')

task 'functional:test', 'Run functional test (node must be running)', -> utils.runTest('test/functional')

task 'watch:js', 'Watch prod source files and build changes', ->
    invoke 'build:js'
    utils.watch_and_run appFiles, 'build:js'

task 'build:js', 'Build single application file from source files', ->
  appContents = new Array 
  remaining = appFiles.length

  for file, index in appFiles then do (file, index) ->
    fs.readFile file, 'utf8', (err, fileContents) ->
      throw err if err
      appContents[index] = fileContents
      process() if --remaining is 0
  process = ->
    fs.writeFile 'public/js/app.coffee', appContents.join('\n\n'), 'utf8', (err) ->
      throw err if err
      exec 'coffee --compile public/js/app.coffee', (err, stdout, stderr) ->
        if err
          console.log "Error compiling coffee file. #{err}"
        else
          fs.unlink 'public/js/app.coffee', (err) ->
            if err
              console.log 'Couldn\'t delete the app.coffee file/'
            console.log 'Done building coffee file.'
          invoke 'compress-client-js'

task 'watch:less', 'Watches public/stylesheets/less for changes and recompiles', ->
  invoke "compile-less"
  less_files = fs.readdirSync(less_dir).map (file) -> "#{less_dir}#{file}"
  utils.watch_and_run less_files, "compile-less"

task 'compile-less', 'Compile and minify CSS', ->
  exec "lessc #{less_dir}main.less -yui-compress > public/stylesheets/style.css"

task 'compress-client-js', 'Use Uglify.js to compress the JS', ->
  console.log "Ugliying client side Javascript"
  exec 'uglifyjs -o public/js/app.min.js public/js/app.js'